ERC20的标准只包括接口。

只要代码满足了IECR20的所有接口就可以，具体逻辑不设置标准。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.18;

/**
 * @dev Interface of the ERC-20 standard as defined in the ERC.
 */
interface IERC20 {
    // 返回当前合约token总量
    function totalSupply() external view returns (uint256);

    // 某个账户的当前余额
    function balanceOf(address account) external view returns (uint);

    // 由当前调用者向另一个账户发送token
    // 是写入的方法，所以会想链外汇报一个transfer事件
    function transfer(address recipient, uint256 amount) external returns (bool);

    // 查询某一个账户对另一个账户的批准额度
    function allowance(address owner, address spender) external view returns (uint256);

    // 批准，把我账户的一定的金额给另一个账户
    function approve(address spender, uint256 value) external returns (bool);

    // 向另一个合约存款的时候调用，transferFrom才能把token拿到合约中，和approve联合使用
    function transferFrom(address from, address to, uint256 value) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

contract ERC20 is IERC20{
    // 总量
    uint public totalSupply;
    // 一个地址对应一个数字,组成了一个账本
    mapping (address => uint) public balanceOf;
    //approve 的映射
    mapping (address =>mapping (address=>uint)) public allowance;

    string public name="Test";
    string public symbol="TEST";
    //定义精度
    //常用18个经典,代表一个1后面会有18个0的小数,
    // 智能合约所记录的数字只能有整数不能有小数,
    // 0.5怎么办,记录为一个5加上17个0
    // 一个1加上18个0代表整数1
    uint8 public decimals=18;

    /**
    转账:
    在发送者账户减去一定数量
    在接收者余额增加一定数量
    **/
    function transfer(address recipient, uint256 amount) external returns (bool){
        balanceOf[msg.sender]-=amount;
        balanceOf[recipient]+=amount;
        emit Transfer(msg.sender, recipient, amount);
        return true;
    }
    /**
    授权
    **/
     function approve(address spender, uint amount) external  returns (bool){
        //找到当前账户和被授权账户
        allowance[msg.sender][spender]=amount;
        emit Approval(msg.sender, spender, amount);
        return true;
     }
    /**
    参数:发送者,接收者,数量
        这个函数的调用者对应的是approve中的被批准的账户
        发送者是approve中的调用者

    **/
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool){
        allowance[sender][msg.sender]-=amount;
        balanceOf[sender]-=amount;

        balanceOf[recipient]+=amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }
    // 简化的铸币,严谨来说需要权限控制
    function mint(uint amount) external {
        balanceOf[msg.sender]+=amount;
        totalSupply+=amount;
        emit Transfer(address(0), msg.sender, amount);
    }
    //销毁方法
    function burn(uint amount) external {
        balanceOf[msg.sender]-=amount;
        totalSupply-=amount;
        emit Transfer(msg.sender, address(0), amount);
    }

}
```



